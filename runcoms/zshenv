#
# Defines environment variables.
#
# Authors:
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#

#
# Browser
#

if [[ "$OSTYPE" == darwin* ]]; then
  export BROWSER='open'
fi

#
# Editors
#

export EDITOR='vim'
export VISUAL='vim'
export PAGER='less'

#
# Language
#

if [[ -z "$LANG" ]]; then
  export LANG='en_US.UTF-8'
fi

#
# Paths
#

typeset -gU cdpath fpath mailpath path
typeset -gUT CPATH cpath
typeset -gUT LIBRARY_PATH library_path
typeset -gUT INFOPATH infopath
typeset -gU MANPATH manpath

# Set the the list of directories that cd searches.
# cdpath=(
#   $cdpath
# )

# Set the list of directories that Zsh searches for programs.
path=(
  /opt/local/{bin,sbin}
  /usr/local/{bin,sbin}
  $path
  ~/bin
)

#
# Less
#

# Set the default Less options.
# Mouse-wheel scrolling has been disabled by -X (disable screen clearing).
# Remove -X and -F (exit if the content fits on one screen) to enable it.
export LESS='-F -g -i -M -R -S -w -X -z-4'

# Set the Less input preprocessor.
if (( $+commands[lesspipe.sh] )); then
  export LESSOPEN='| /usr/bin/env lesspipe.sh %s 2>&-'
fi

case `uname` in
    *Darwin*)
        export ARDUINO_DIR='/Applications/Arduino.app/Contents/Resources/Java'
        export ARDMK_DIR='~/bin/Arduino-Makefile'
        path=($path ~/Library/Python/2.7/bin ~/bin/Arduino-Makefile/bin /opt/local/Library/Frameworks/Python.framework/Versions/2.7/bin/ /Applications/MATLAB_R2010b.app/bin/)
        ;;
    *Linux*)
        export PYENV_ROOT="${HOME}/.pyenv"
        if [ -d "${PYENV_ROOT}" ]; then
            path=("${PYENV_ROOT}/bin" $path)
            eval "$(pyenv init -)"
        fi
        path=(/usr/local/texlive/2014/bin/x86_64-linux $path)
        infopath=(/usr/local/texlive/2014/texmf-dist/doc/info $infopath)
        manpath=(/usr/local/texlive/2014/texmf-dist/doc/man $manpath)
        ;;
esac

cpath=(/opt/local/include $cpath)
library_path=(/opt/local/lib $library_path)

#
# Temporary Files
#

if [[ ! -d "$TMPDIR" ]]; then
  export TMPDIR="/tmp/$USER"
  mkdir -p -m 700 "$TMPDIR"
fi

TMPPREFIX="${TMPDIR%/}/zsh"
if [[ ! -d "$TMPPREFIX" ]]; then
  mkdir -p "$TMPPREFIX"
fi

